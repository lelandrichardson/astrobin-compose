plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.example.astrobin"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // Support both local.properties or ~/.gradle/gradle.properties
        def properties = new Properties()
        if (rootProject.file("local.properties").exists()) {
            properties.load(rootProject.file("local.properties").newDataInputStream())
        }
        buildConfigField(
                "String",
                "ASTROBIN_API_KEY",
                "\"${properties["ASTROBIN_API_KEY"] ?: property("ASTROBIN_API_KEY")}\""
        )
        buildConfigField(
                "String",
                "ASTROBIN_API_SECRET",
                "\"${properties["ASTROBIN_API_SECRET"] ?: property("ASTROBIN_API_SECRET")}\""
        )
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:generateFunctionKeyMetaClasses=true",
                "-opt-in=kotlin.RequiresOptIn"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.1"
    implementation "androidx.navigation:navigation-compose:2.5.1"
    implementation "androidx.paging:paging-compose:1.0.0-alpha15"
    implementation "com.google.accompanist:accompanist-flowlayout:0.20.2"
    implementation "com.google.accompanist:accompanist-insets-ui:0.20.2"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.moshi:moshi:1.11.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.11.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"
    implementation "io.coil-kt:coil-compose:2.1.0"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.paging:paging-common-ktx:3.1.1'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}